/*
 * Copyright (c) 2019, Seqera Labs.
 *
 * This Source Code Form is subject to the terms of the Mozilla Public
 * License, v. 2.0. If a copy of the MPL was not distributed with this
 * file, You can obtain one at http://mozilla.org/MPL/2.0/.
 *
 * This Source Code Form is "Incompatible With Secondary Licenses", as
 * defined by the Mozilla Public License, v. 2.0.
 */

plugins {
    id "io.spring.dependency-management" version "1.0.6.RELEASE"
    id "groovy"
    id "com.github.johnrengelman.shadow" version "4.0.2"
    id "application"
    id "com.google.cloud.tools.jib" version "0.9.9"
    id 'com.palantir.git-version' version '0.11.0'
}

version "0.1"
group "io.seqera"

repositories {
    mavenCentral()
    maven { url "https://jcenter.bintray.com" }
}

dependencyManagement {
    imports {
        mavenBom 'io.micronaut:micronaut-bom:1.2.0'
    }
}

configurations {
    // for dependencies that are needed for development only
    developmentOnly 
}

dependencies {
    compile "io.micronaut:micronaut-runtime-groovy"
    compile "io.micronaut:micronaut-validation"
    compile "io.micronaut:micronaut-http-client"
    compile "io.micronaut:micronaut-http-server-netty"
    
    compile "io.micronaut.configuration:micronaut-hibernate-gorm"
    runtime 'mysql:mysql-connector-java:8.0.16'
    runtime "com.h2database:h2:1.4.199"
    
    compile "javax.activation:activation:1.1.1"
    compile "javax.mail:mail:1.4.7"
    compile "org.jsoup:jsoup:1.11.2"
    compile "org.apache.commons:commons-text:1.6"
    compile "org.codehaus.groovy:groovy-templates:2.5.3"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.9.8"
    compile "io.github.resilience4j:resilience4j-ratelimiter:0.17.0"
    
    compileOnly "io.micronaut:micronaut-inject-groovy"

    compile "io.micronaut:micronaut-management"
    compile "io.micronaut.configuration:micronaut-micrometer-core"
    compile "io.micronaut.configuration:micronaut-micrometer-registry-statsd"
    
    annotationProcessor "io.micronaut:micronaut-security"
    compile "io.micronaut:micronaut-security-jwt"
    
    compile 'io.micronaut.configuration:micronaut-flyway:1.1.0'
    compile "ch.qos.logback:logback-classic:1.2.3"

    annotationProcessor "io.micronaut.configuration:micronaut-openapi"
    compile "io.swagger.core.v3:swagger-annotations"

    testCompile "io.micronaut:micronaut-inject-groovy"
    testCompile("org.spockframework:spock-core") {
        exclude group: "org.codehaus.groovy", module: "groovy-all"
    }
    testCompile "io.micronaut.test:micronaut-test-spock:1.0.3"

    testCompile "org.testcontainers:testcontainers:1.11.2"
    testCompile "org.testcontainers:spock:1.11.2"
    testCompile "org.subethamail:subethasmtp:3.1.7"
    testCompile 'net.bytebuddy:byte-buddy:1.9.12'
    testCompile "org.objenesis:objenesis:2.1"

    if (System.getProperty('_system_type')=='Darwin') {
        // https://docs.micronaut.io/latest/guide/index.html#automaticRestart
        developmentOnly "io.micronaut:micronaut-runtime-osx"
        developmentOnly "net.java.dev.jna:jna"
        developmentOnly "io.methvin:directory-watcher"
    }
}

test.classpath += configurations.developmentOnly

mainClassName = "io.seqera.tower.Application"
tasks.withType(GroovyCompile) {
    groovyOptions.forkOptions.jvmArgs.add('-Dgroovy.parameters=true')
}

afterEvaluate {
    def info = """\
        name=${name}
        group=${group}
        version=${version}
        commitId=${gitVersion()}
        """.stripIndent().toString()
    def f = file("src/main/resources/META-INF/build-info.properties")
    f.parentFile.mkdir()
    f.text = info
}

//Only for the main (not test) source set
compileGroovy.groovyOptions.configurationScript = file('src/groovyCompile/groovycConfig.groovy')

shadowJar {
    mergeServiceFiles()
}

run.classpath += configurations.developmentOnly
run.jvmArgs('-noverify', '-XX:TieredStopAtLevel=1', '-Dcom.sun.management.jmxremote')
jib.to.image = 'gcr.io/tower-backend/jib-image'
